I want window contains LISTBOX and the window title is Using ADODB.
Window width = 600.  and WINDOW HEIGHT = 500. 
Window back color is white.

Listbox width = 350. and LISTBOX HEIGHT = 450.
Listbox back color is silver.
Listbox fore color is Black.
Listbox font name is arial.
Listbox font size is 14.

I want button and button caption is Test ADODB. 
Button name = btn1. and button LEFT = 400. and button width = 150.

procedure btn1 mouse click.

	:Note: " Connect to the Database Test.MDB"
	[MYCON]. COM OBJECT TO CREATE OF TYPE "(ADODB.Connection)"
	[MYCON]. COM Object to use
	[MYARRAY]. = (1).
	[MYARRAY]\(1)\(1). = (string). 
	[MYARRAY]\(1)\(2). = '(Provider=MSDASQL.1;Persist Security Info=False;Extended Properties="dsn=MS Access Database;uid=";Initial Catalog=test.mdb)'
	"(OPEN)" COM method to call with parameters MYARRAY.

	:Note: " Open Table MainTable"
	[MYRS]. COM OBJECT TO CREATE OF TYPE "(ADODB.Recordset)"
	[MYRS]. COM Object to use
	(CursorType). COM Property to set (3).
	(LockType). COM Property to set (3).
	(ActiveConnection). COM Property to set [MYCON].
	[MYARRAY]. = (1).
	[MYARRAY]\(1)\(1). = (String). 
	[MYARRAY]\(1)\(2). = '(Select * from MainTable)'
	"(OPEN)" COM method to call with parameters MYARRAY.

	:Note: " Delete All Records"
	'(RECORDCOUNT)' COM PROPERTY TO GET [MYMAX].
	doif [mymax] > (0).
		[X]. = (1). 
		DoWhile [x] <= [MYMAX].
			"(movefirst)" COM method to call with parameters .
			"(delete)" COM method to call with parameters .
			"(update)" COM method to call with parameters .
			[x]. = [x] + (1).
		endwhile
	endif
		
	:Note: " Add 10 Records "	
	[X]. = (1). 
	DoWhile [x] <= (10).
		"(AddNew)" com method to call with parameters .
		'|fields:item("Number"):value|' COM PROPERTY TO SET [x].
		'|fields:item("Square"):value|' COM PROPERTY TO SET [x] x [x].
		"(UPDATE)" COM method to call with parameters .
		[x]. = [x] + (1).
	EndWhile
	
	:Note: " Show All Records "
	"(movefirst)" COM method to call with parameters .
	'(RECORDCOUNT)' COM PROPERTY TO GET [MYMAX].
	listbox must add item --------------------------.
	listbox must add from (Records Count = ) [MYMAX].
	listbox must add item --------------------------.
	listbox must add item Number 		     Square.
	listbox must add item --------------------------.
	[X]. = (1). 
	DoWhile [x] <= [MYMAX].
		'|fields:item("Number"):value|' COM PROPERTY TO GET [V1].
		'|fields:item("Square"):value|' COM PROPERTY TO GET [V2].
		Listbox must add from [v1] (                      ) [v2].
		"(movenext)" COM method to call with parameters .
		[x]. = [x] + (1).
	EndWhile
	
	:Note: " Close Recordset"
	"(CLOSE)" COM method to call with parameters .

	:Note: " Close Connection"
	[MYCON]. COM Object to use and "(CLOSE)" COM method to call with parameters .

endprocedure
